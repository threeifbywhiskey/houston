HAI 1.3

HOW IZ I palindromin YR num
    I HAS A a ITZ QUOSHUNT OF num AN 100000
    I HAS A b ITZ MOD OF QUOSHUNT OF num AN 10000 AN 10
    I HAS A c ITZ MOD OF QUOSHUNT OF num AN  1000 AN 10
    I HAS A d ITZ MOD OF QUOSHUNT OF num AN   100 AN 10
    I HAS A e ITZ MOD OF QUOSHUNT OF num AN    10 AN 10
    I HAS A f ITZ MOD OF num AN 10

    BOTH SAEM a AN f, O RLY?
        YA RLY
            BOTH SAEM b AN e, O RLY?
                YA RLY
                    BOTH SAEM c AN d, O RLY?
                        YA RLY, FOUND YR WIN
                        NO WAI, FOUND YR FAIL
                    OIC
                NO WAI, FOUND YR FAIL
            OIC
        NO WAI, FOUND YR FAIL
    OIC
IF U SAY SO

I HAS A max ITZ 0
I HAS A product ITZ 0

IM IN YR outer UPPIN YR a TIL BOTH SAEM a AN 100
    IM IN YR inner UPPIN YR b TIL BOTH SAEM b AN 100
        product R PRODUKT OF SUM OF a AN 900 AN SUM OF b AN 900
        I IZ palindromin YR product MKAY, O RLY?
            YA RLY
                BOTH SAEM product AN BIGGR OF product AN max, O RLY?
                    YA RLY, max R product
                OIC
        OIC
    IM OUTTA YR inner
IM OUTTA YR outer

VISIBLE max
KTHXBYE
